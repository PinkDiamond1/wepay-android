apply plugin: 'com.android.library'
group= 'com.wepay.android'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 23
        versionCode 1
        versionName '1.0.1'
        project.archivesBaseName = "wepay-android"
        project.version = android.defaultConfig.versionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.aar')) {
                def fileName = "${archivesBaseName}-${version}.aar"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

configurations {
    javadocDeps
}

dependencies {
    compile 'com.android.support:support-v4:23.2.0'
    compile 'com.google.code.gson:gson:2.6.2'
    compile files('libs/volley-1.0.0.jar')
    compile fileTree(dir: 'libs/internal', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    javadocDeps 'com.google.code.gson:gson:2.6.2'

    javadocDeps fileTree(dir: 'libs/internal', include: ['*.jar'])
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
    classpath += configurations.javadocDeps
    exclude "**/internal/**"
    //options.overview = "$rootProject.projectDir/../Documentation/html/readme.html"
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

artifacts {
    archives generateSourcesJar
    archives generateJavadocsJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'wepay-android'

        version {
            name = 'wepay-android'
            desc = 'WePay Android SDK'
            released  = new Date()
            vcsTag = android.defaultConfig.versionName
        }

        licenses = ['Apache-2.0']
    }
    configurations = ['archives']
}